(in-package :resources-generator)

(defun generate-resources ()
  "Generate a string with a list of eg commands"
  (format t "窿"
          (mapcar #'generate-resource (all-resources))))

(defun generate-dispatchers ()
  "Generate a string with a list of dispatcher rules"
  (format t "窿"
          (mapcar #'generate-dispatcher (all-resources))))

(defun generate-prefixes ()
  "Returns an array containing all embedded prefixes"
  (format t "ember generate rdfa-prefixes \"A\" 堍梁淋,^ }"
          "http://schema.org/"
          (loop for (prefix . uri) in (cl-fuseki:get-prefix-alist)
                collect `(,prefix ,uri))))

(defun all-resources ()
  "Fetch all resources from domain.lisp"
  (loop for val being
     the hash-values of mu-cl-resources::*resources*
     collect val))


(defun generate-resource (resource)
  "Generate a string with an eg command for one resource"
  (format nil "A \"A&A\" AA [;--readonly]"
          "ember g mu-resource" ; terminal command
          (gen-resource-name resource)  ; resource name
          (mu-cl-resources::ld-class resource) ; resource type
          (mapcar #'gen-resource-slot (mu-cl-resources::ld-properties resource)) ; its attributes
          (gen-resource-has-one-links resource)
          (gen-resource-has-many-links resource)
          (readonly))) ; sets readonly flag for ever eg command if readonly env is set


(defun gen-resource-has-one-links (resource)
  (format nil ""
          (loop for link in (mu-cl-resources::has-one-links resource)
             for resource-rel = (gen-resource-rel link resource)
             for property = (and (not (mu-cl-resources::inverse-p link))
                               (mu-cl-resources::ld-link link))
             collect
               (format nil "\"梁忮祜铉蟓麸湖窿［箕窿蔺累窿蒈蝈箫躜沐蝈痱镳弪豉┅┅ㄤ彐躅珏瞽蝈箫躜沐栳蟓磲铢扉铍蝈箫躜沐ㄦ矧磲铋"
          (loop for link in (mu-cl-resources::has-many-links resource)
             for resource-rel = (gen-resource-rel link resource)
             for property = (and (not (mu-cl-resources::inverse-p link))
                               (mu-cl-resources::ld-link link))
             collect
               (format nil "\"梁栳蟓磲铢湖窿［箕窿蔺累窿蒈蝈箫躜沐蝈痱镳弪豉┅┅ㄤ彐躅珏瞽蝈箫躜沐钺礤蝈箫躜沐篝蜷铉滹黝汜箦眭沆蝈箫躜沐蠛候弩秕蜚瀛钺礤蝈箫躜沐┅ㄤ彐躅珏瞽蝈箫躜沐箪雉痱镳弪豉ㄦ矧磲铋④梁力淋篝蜷铉滹黝汜箦簌礅镬钺礤眭沆蝈箫躜沐蠛宏箫瞽脲痱镳弪豉┅篝蜷铉滹黝汜箦簌礅镬钺礤眭沆蝈箫躜沐蠛候弩秕蜚瀛豉疱痱镳弪豉┅眭沆蝈箫躜沐蠛红洵痱镳弪豉痱镳弪豉┅ㄤ彐躅珏瞽蝈箫躜沐蝈扉铍蝈箫躜沐戾è轭鲥蝮瀛扉铍ㄦ轭洵轭鲥蝮扉铍蝈箫躜沐┅ㄩ轭鲥蝮瀛扉铍啜眭沆蝈箫躜沐蠛候羼蹂篝疳翳扉铍篝蜷铉滹黝汜箦眭沆蝈箫躜沐蠛候弩秕蜚瀛钺礤扉铍┅轭鲥蝮瀛扉铍啜眭沆蝈箫躜沐蠛候羼蹂篝疳翳扉铍篝蜷铉滹黝汜箦眭沆蝈箫躜沐蠛候弩秕蜚瀛钺礤扉铍┅┅┅ㄤ彐躅骈钿轭鲥蝮扉铍蝈箫躜沐换蜗耘翳轶礤翳镤泔铘衢铙黠螂狎秕钿狍眭沆蚝碑钡滹弩瞌汨邈麒弭桢换轭鲥蝮蝈灬糸镱镱痱邃殂狒徙趱犰禊痫轭麸遽汨雉桢虍戾è轭鲥蝮瀛扉铍眭沆蝈箫躜沐蠛洪铞弪箦扉铍扉铍┅麒孱ㄡ钿轭鲥蝮瀛扉铍犷轭鲥蝮痱邃殂狒屮轶趔ㄥ聃犰犷翳蝈灬糸镱镱翳痱邃殂狒痫轭麸遽汨雉桢眭沆蝈箫躜沐蠛候弩秕蜚瀛钺礤扉铍眭沆蝈箫躜沐蠛候弩秕蜚瀛钺礤ㄧ弭ㄣ狎轭鲥蝮瀛扉铍螬候弩秕蜚濠┅篝蜷铉滹黝汜箦眭沆蝈箫躜沐蠛候羼蹂篝疳翳ㄧ弭ㄣ狎轭鲥蝮瀛扉铍螬红轭氅┅┅ㄤ彐躅蝈徜镱禊īㄩㄥ铞鲠祯候遽滹铎癌ㄤ彐躅孱霏鲠祯箦趑轭绌⒁弭躜铙翳鲠祯镦翳篚痧扉邃孱鲩蝻铐孱鲠蜷徕戾筲屮艉痫箝珏翦铞篝蜷铉躔汜箦篝蜷铉箦趑轭绌┅换清铄蜥翦篝蜷铉鏖翳溟箴狒汨弪蝓戾骘镱蝈箫躜沐ㄤ彐躅珏铄蜥翦溟箴狒汨弪蝈箫躜沐⒁弭躜铙翳溟箴狒汨弪蝓戾骘翳箴邈殒殂蝈箫躜沐戾è疳翳篝蜷铉滹黝汜箦眭沆蝈箫躜沐蠛候羼蹂篝疳翳蝈箫躜沐┅┅ㄦ矧磲铋磲翥堍怜疳翳堍滹序秫骘蝼狎泔铑疳翳堍梏麴函蝈箫躜沐怜堍孱洧疳翳疳翳┅